pkgname=openssh-peerbox
pkgver=6.7p1
pkgrel=2
pkgdesc='Free version of the SSH connectivity tools'
provides=('openssh')
conflicts=('openssh')
url='http://www.openssh.org/portable.html'
license=('custom:BSD')
arch=('armv6h' 'i686' 'x86_64')
depends=('openssl' 'libedit')
source=(ftp://ftp.openbsd.org/pub/OpenBSD/OpenSSH/portable/openssh-${pkgver}.tar.gz
        'ssh-genkeys.service'
        'sshd.service'
        'sshd.pam'
		'sshd_config'
		)

sha256sums=('b2f8394eae858dabbdef7dac10b99aec00c95462753e80342e530bbb6f725507'
            '8b2ead71715c235cce50c653c97d8eb54bb2908c6fd8c09cbaf20ef9a1c6196c'
            '41347824023de76399cd9b7b117e30dc976c75803711c6006b70ff23998adff4'
            '64576021515c0a98b0aaf0a0ae02e0f5ebe8ee525b1e647ab68f369f81ecd846'
            '3dd8a0819d73fe92c9e69430284d5c55713315f158f3b1b7fd7b475b9775ba15')

backup=('etc/ssh/ssh_config' 'etc/ssh/sshd_config')

build() {
	cd "${srcdir}/openssh-${pkgver}"

	./configure \
		--prefix=/usr \
		--sbindir=/usr/bin \
		--libexecdir=/usr/lib/ssh \
		--sysconfdir=/etc/ssh \
		--with-libedit \
		--with-ssl-engine \
		--with-pam \
		--with-privsep-user=nobody \
		--with-md5-passwords \
		--with-pid-dir=/run \

	make
}

package() {
	
	## Use preconfigured sshd_config

	cp sshd_config ${srcdir}/openssh-${pkgver}

	cd "${srcdir}/openssh-${pkgver}"

	make DESTDIR="${pkgdir}" install

	install -Dm644 LICENCE "${pkgdir}/usr/share/licenses/${pkgname}/LICENCE"
	
	install -Dm644 ../ssh-genkeys.service "${pkgdir}"/usr/lib/systemd/system/ssh-genkeys.service
	install -Dm644 ../sshd.service "${pkgdir}"/usr/lib/systemd/system/sshd.service
	install -Dm644 ../sshd.pam "${pkgdir}"/etc/pam.d/sshd

	install -Dm755 contrib/findssl.sh "${pkgdir}"/usr/bin/findssl.sh
	install -Dm755 contrib/ssh-copy-id "${pkgdir}"/usr/bin/ssh-copy-id
	install -Dm644 contrib/ssh-copy-id.1 "${pkgdir}"/usr/share/man/man1/ssh-copy-id.1

}